apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

android {
    signingConfigs {
        defaultSign {
            storeFile file("../weather.jks")
            storePassword System.env.WEATHER_PASSWORD ?: WEATHER_PASSWORD
            keyAlias = System.env.WEATHER_ALIAS ?: WEATHER_ALIAS
            keyPassword System.env.WEATHER_PASSWORD ?: WEATHER_PASSWORD
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.elxreno.weather"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "openWeatherMapKey", System.env.OPENWEATHERMAP_KEY ?: openWeatherMapKey
    }
    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.defaultSign
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.defaultSign
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // Kotlin stuff
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX stuff
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'

    // Material stuff
    implementation "com.google.android.material:material:1.1.0"

    // Tests stuff
    testImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

    // Moxy
    ext.moxyVersion = "1.7.0"
    implementation "tech.schoolhelper:moxy-x:$moxyVersion"
    implementation "tech.schoolhelper:moxy-x-androidx:$moxyVersion"
    kapt "tech.schoolhelper:moxy-x-compiler:$moxyVersion"

    // Anko
    ext.ankoVersion = "0.10.8"
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"

    // Retrofit
    ext.retrofitVersion = "2.7.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // OkHttp logging
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'

    // Room
    ext.roomVersion = "2.2.5"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Dagger
    ext.daggerVersion = "2.27"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
}
